plugins {
    id 'idea'
    id 'io.freefair.lombok' version '6.5.0.3' apply(false)
    id 'io.spring.javaformat' version '0.0.39' apply(false)
    id 'io.spring.dependency-management' version '1.1.0' apply(false)
    id 'com.github.hierynomus.license' version '0.15.0' apply(false)
}

subprojects {
    apply plugin: 'idea'
//    apply plugin: 'checkstyle'
    apply plugin: 'java-library'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'io.spring.javaformat'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.github.hierynomus.license'

    ext {
        set('springVersion', '3.1.2')
    }

    group = 'com.konfigyr'
    version = '0.0.1'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
        mavenLocal()
    }

    idea {
        module {
            outputDir = compileJava.destinationDirectory.asFile.get()
            testOutputDir = compileTestJava.destinationDirectory.asFile.get()
        }
    }

    dependencies {
//        checkstyle 'io.spring.javaformat:spring-javaformat-checkstyle:0.0.39'

        annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.boot:spring-boot-starter-validation'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springVersion}"
        }
    }

    license {
        header rootProject.file('LICENCE')
        strictCheck true
    }

    test {
        useJUnitPlatform()
    }
}
